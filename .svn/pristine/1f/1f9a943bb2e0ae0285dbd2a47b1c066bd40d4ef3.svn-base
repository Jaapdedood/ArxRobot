package vo
{
	import com.smartfoxserver.v2.entities.data.ISFSObject;
	import flash.utils.ByteArray;
	
	/**
	 * Used with Camera Move message to transmit Camera Pan and/or Tilt requests.
	 * Note: Superclass implements IExternalizable.
	 */
	public class CameraMove extends ArduinoCommand
	{
		// PUBLIC PROPERTIES AND GET/SET METHOD GROUPS
		
		/**
		 * Pan degrees
		 */
		public function get panDegrees ( ) : uint
		{
			_commandBytes.position = 1;
			return _commandBytes.readUnsignedShort ( );
		}
		public function set panDegrees ( value:uint ) : void
		{
			_commandStringClear ( );
			_commandBytes.position = 1;
			_commandBytes.writeShort ( value );
		}
		
		/**
		 * Tilt degrees
		 */
		public function get tiltDegrees ( ) : uint
		{
			_commandBytes.position = 3;
			return _commandBytes.readUnsignedShort ( );
		}
		public function set tiltDegrees ( value:uint ) : void
		{
			_commandStringClear ( );
			_commandBytes.position = 3;
			_commandBytes.writeShort ( value );
		}
		
		// CONSTRUCTOR
		
		/**
		 * Call static method <strong>newFromParameters</strong> or <strong>newFromSFSObject</strong>, as appropriate
		 * @param commandBytes
		 */		
		public function CameraMove ( commandBytes:ByteArray = null )
		{
			super ( commandBytes );
		}
		
		// PUBLIC STATIC METHODS
		
		public static function NewFromParameters (
			panDegrees:uint = 0,
			tiltDegrees:uint = 0
		) : CameraMove
		{
			var ba:ByteArray = new ByteArray ( );
			ba.writeByte ( CAMERA_MOVE );
			ba.writeShort ( panDegrees );
			ba.writeShort ( tiltDegrees );
			return new CameraMove ( ba );
		}
		
		public static function NewFromSFSObject ( sfso:ISFSObject ) : CameraMove
		{
			return new CameraMove ( sfso.getByteArray ( 'b' ) );
		}
	}
}
