package utils
{
	import com.smartfoxserver.v2.SmartFox;
	import com.smartfoxserver.v2.core.SFSEvent;
	import com.smartfoxserver.v2.entities.Room;
	import com.smartfoxserver.v2.entities.User;
	import com.smartfoxserver.v2.entities.data.ISFSObject;
	import com.smartfoxserver.v2.entities.variables.SFSRoomVariable;
	import com.smartfoxserver.v2.entities.variables.SFSUserVariable;
	import com.smartfoxserver.v2.redbox.events.RedBoxConnectionEvent;
	import com.smartfoxserver.v2.requests.CreateRoomRequest;
	import com.smartfoxserver.v2.requests.JoinRoomRequest;
	import com.smartfoxserver.v2.requests.LoginRequest;
	import com.smartfoxserver.v2.requests.LogoutRequest;
	import com.smartfoxserver.v2.requests.PrivateMessageRequest;
	import com.smartfoxserver.v2.requests.RoomEvents;
	import com.smartfoxserver.v2.requests.RoomPermissions;
	import com.smartfoxserver.v2.requests.RoomSettings;
	import com.smartfoxserver.v2.requests.SetRoomVariablesRequest;
	import com.smartfoxserver.v2.requests.SetUserVariablesRequest;
	import com.smartfoxserver.v2.util.ConfigData;
	
	import flash.events.Event;
	import flash.events.TimerEvent;
	import flash.filesystem.File;
	import flash.filesystem.FileMode;
	import flash.filesystem.FileStream;
	import flash.geom.Matrix;
	import flash.media.Video;
	import flash.net.NetStream;
	import flash.system.Security;
	import flash.utils.Timer;
	
	import mx.collections.ArrayList;
	
	import away3d.core.math.MathConsts;
	
	import controllers.CameraManager;
	import controllers.SessionManager;
	import controllers.WaypointsManager;
	
	import icons.IconModeSmRobotCS;
	
	import vo.CameraConfig;
	import vo.CameraMove;
	import vo.EmergencyFlags;
	import vo.MessageByteArray;
	import vo.MessageData;
	import vo.MoveProps;
	import vo.Ping;
	import vo.SfsPreset;
	import vo.UserState;
	import vo.WaypointCoordinates;
	import vo.WaypointsList;
	
	/**
	 * Control connector for the Client-Server operational
	 * mode, which depends upon the SmartFoxServer.
	 */
	[Bindable]
	public class ControlConnectorCS extends ControlConnector
	{
		// CONSTANTS
		
		private const _ROOM_VAR_MSECS:int = 6000;
		private const _USER_VAR_MSECS:int = 500;
		
		
		// PUBLIC PROPERTIES AND GET/SET METHOD GROUPS
		
		private var _connectPreset:SfsPreset;
		public function get connectPreset():SfsPreset
		{
			return _connectPreset;
		}
		public function set connectPreset(value:SfsPreset):void
		{
			_connectPreset = value;
			if ( value )
			{
				var cfd:ConfigData = value.configData;
				connectSettings.blueBoxPollingRate = cfd.blueBoxPollingRate;
				connectSettings.debug = cfd.debug;
				connectSettings.host = cfd.host;
				connectSettings.httpPort = cfd.httpPort;
				connectSettings.port = cfd.port;
				connectSettings.udpHost = cfd.udpHost;
				connectSettings.udpPort = cfd.udpPort;
				connectSettings.useBlueBox = cfd.useBlueBox;
				connectSettings.zone = cfd.zone;
			}
		}
		
		public function get connectPresetsList ( ) : ArrayList
		{
			return new ArrayList ( _aPresets );
		}
		
		override public function get icon ( ) : Object
		{
			return IconModeSmRobotCS;
		}
		
		public var connectPresetTypical:SfsPreset;
		public var connectPrompt:String;
		public var connectSettings:SfsPreset;
		public var loginAssetsDir:String;
		public var loginPilotNames:String;
		public var loginPrompt:String;
		public var loginRobotName:String;
		
		
		// CONSTRUCTOR / DESTRUCTOR
		
		/**
		 * @copy ControlConnectorSmartFox
		 */
		public function ControlConnectorCS ( )
		{
			super ( );
			// NOTE: Do not call init() here, because it
			// is called in the superclass constructor.
		}
		
		override public function dismiss ( ) : void
		{
			_wpsMgr = null;
			
			_ConnectTimerDismiss ( );
			
			_sessionMgr.removeEventListener ( SessionManager.APP_RESIZED, _AppResized );
			
			if ( _tmrDog )
			{
				_tmrDog.stop ( );
				_tmrDog.removeEventListener ( TimerEvent.TIMER, _WatchDogBark );
				_tmrDog = null;
			}
			
			if ( _tmrRoomVars )
			{
				_tmrRoomVars.stop ( );
				_tmrRoomVars.removeEventListener ( TimerEvent.TIMER, _RoomVarsQueuedSend );
				_tmrRoomVars = null;
			}
			
			if ( _tmrUserVars )
			{
				_tmrUserVars.stop ( );
				_tmrUserVars.removeEventListener ( TimerEvent.TIMER, _UserVarsQueuedSend );
				_tmrUserVars = null;
			}
			
			if ( _camMgr )
			{
				_camMgr.removeEventListener ( CameraManager.CAMERA_CHANGED, _AvSenderCameraChanged );
				_camMgr.removeEventListener ( CameraManager.CAMERA_CONFIG_CHANGED, _AvSenderConfigChanged );
				_camMgr.removeEventListener ( CameraManager.CAMERA_DIMENSIONS_READY, _AvSenderDimensionsReady );
				_camMgr.removeEventListener ( CameraManager.CAMERA_INSET_ALIGN_H, _AvSenderMonitorUpdate );
				_camMgr.removeEventListener ( CameraManager.CAMERA_INSET_ALIGN_V, _AvSenderMonitorUpdate );
				_camMgr.removeEventListener ( CameraManager.CAMERA_INSET_SIZE, _AvSenderMonitorUpdate );
				_camMgr.removeEventListener ( CameraManager.CAMERA_MONITOR, _AvSenderMonitorUpdate );
				_camMgr.enabled = false;
				_camMgr = null;
			}
			
			if ( _sf )
			{
				_AvCastMgrDismiss ( );
				
				// SmartFox event listeners
				_sf.removeEventListener ( SFSEvent.CONNECTION, _Connection );
				_sf.removeEventListener ( SFSEvent.CONNECTION_LOST, _ConnectionLost );
				_sf.removeEventListener ( SFSEvent.PING_PONG, _LagMonitorUpdate );
				_sf.removeEventListener ( SFSEvent.LOGIN_ERROR, _LoginFailed );
				_sf.removeEventListener ( SFSEvent.LOGIN, _LoginSucceeded );
				_sf.removeEventListener ( SFSEvent.LOGOUT, _LogoutDone );
				_sf.removeEventListener ( SFSEvent.PRIVATE_MESSAGE, _PrivateMessageReceived );
				_sf.removeEventListener ( SFSEvent.ROOM_CREATION_ERROR, _RoomCreationError );
				_sf.removeEventListener ( SFSEvent.ROOM_JOIN, _JoinSuccess );
				_sf.removeEventListener ( SFSEvent.ROOM_JOIN_ERROR, _JoinFailure );
				_sf.removeEventListener ( SFSEvent.USER_COUNT_CHANGE, _UserCountChanged );
				_sf.removeEventListener ( SFSEvent.USER_ENTER_ROOM, _UserEnteredRoom );
				_sf.removeEventListener ( SFSEvent.USER_EXIT_ROOM, _UserExitedRoom );
				
				if ( _sf.isConnected )
				{
					if ( _sf.lastJoinedRoom )
					{
						_sf.send ( new LogoutRequest ( ) );
					}
					_sf.disconnect ( );
				}
				_sf = null;
			}
			
			super.dismiss ( );
		}
		
		/**
		 * Called automatically by superclass during instantiation,
		 * but may also be called manually to reactivate
		 * if object was previously dismissed.
		 * Subclass overrides must call super.init().
		 */
		override public function init ( ) : void
		{
			super.init ( );
			
			connectSettings = new SfsPreset ( );
			
			var userState:UserState = _sessionMgr.userState;
			loginAssetsDir = userState.assetsDir;
			loginPilotNames = userState.pilotNames;
			loginPrompt = '';
			loginRobotName = userState.robotName;
			
			_tmrUserVars = new Timer ( _USER_VAR_MSECS, 0 );
			_tmrUserVars.addEventListener ( TimerEvent.TIMER, _UserVarsQueuedSend );
			
			_tmrRoomVars = new Timer ( _ROOM_VAR_MSECS, 0 );
			_tmrRoomVars.addEventListener ( TimerEvent.TIMER, _RoomVarsQueuedSend );
			
			// default watch dog to 2.1 secs, then adjust when get current value from ping
			_tmrDog = new Timer ( 2100, 1 );
			_tmrDog.addEventListener ( TimerEvent.TIMER, _WatchDogBark );
			
			_wpsMgr = WaypointsManager.instance;
			
			// instantiate SmartFox client class
			_sf = new SmartFox ( _sessionMgr.debugOn );
			debugOut ( 'status_sfs_api', true, [ _sf.version ] );
			
			// SmartFox event listeners
			_sf.addEventListener ( SFSEvent.CONNECTION, _Connection );
			_sf.addEventListener ( SFSEvent.CONNECTION_LOST, _ConnectionLost );
			_sf.addEventListener ( SFSEvent.PING_PONG, _LagMonitorUpdate );
			_sf.addEventListener ( SFSEvent.LOGIN_ERROR, _LoginFailed );
			_sf.addEventListener ( SFSEvent.LOGIN, _LoginSucceeded );
			_sf.addEventListener ( SFSEvent.LOGOUT, _LogoutDone );
			_sf.addEventListener ( SFSEvent.PRIVATE_MESSAGE, _PrivateMessageReceived );
			_sf.addEventListener ( SFSEvent.ROOM_CREATION_ERROR, _RoomCreationError );
			_sf.addEventListener ( SFSEvent.ROOM_JOIN, _JoinSuccess );
			_sf.addEventListener ( SFSEvent.ROOM_JOIN_ERROR, _JoinFailure );
			_sf.addEventListener ( SFSEvent.USER_COUNT_CHANGE, _UserCountChanged );
			_sf.addEventListener ( SFSEvent.USER_ENTER_ROOM, _UserEnteredRoom );
			_sf.addEventListener ( SFSEvent.USER_EXIT_ROOM, _UserExitedRoom );
			
			_camMgr = CameraManager.instance;
			_camMgr.addEventListener ( CameraManager.CAMERA_CHANGED, _AvSenderCameraChanged );
			_camMgr.addEventListener ( CameraManager.CAMERA_CONFIG_CHANGED, _AvSenderConfigChanged );
			_camMgr.addEventListener ( CameraManager.CAMERA_DIMENSIONS_READY, _AvSenderDimensionsReady );
			_camMgr.addEventListener ( CameraManager.CAMERA_INSET_ALIGN_H, _AvSenderMonitorUpdate );
			_camMgr.addEventListener ( CameraManager.CAMERA_INSET_ALIGN_V, _AvSenderMonitorUpdate );
			_camMgr.addEventListener ( CameraManager.CAMERA_INSET_SIZE, _AvSenderMonitorUpdate );
			_camMgr.addEventListener ( CameraManager.CAMERA_MONITOR, _AvSenderMonitorUpdate );
			_camMgr.enabled = true;
			
			_sessionMgr.addEventListener ( SessionManager.APP_RESIZED, _AppResized );
			
			callLater ( _PresetsLoad );
		}
		
		
		// OTHER PUBLIC METHODS
		
		override public function avSenderClear ( ) : Boolean
		{
			var bWasOn:Boolean = false;
			
			_AvSenderMonitorClear ( );
			
			if ( _nsSndr != null )
			{
				bWasOn = true;
				_avCastMgr.unpublishLiveCast ( );
				_nsSndr.dispose ( );
				_nsSndr = null;
			}
			
			return bWasOn;
		}
		
		override public function avSenderPublish ( ) : void
		{
			avSenderClear ( );
			
			// Publish my live cast
			if ( !_avCastMgr )
				return;
			
			if ( !_avCastMgr.isConnected )
				return;
			
			var bOk:Boolean = true;
			var aParams:Array;
			var sRsrc:String;
			var sStat:String;
			try
			{
				_nsSndr = _avCastMgr.publishLiveCast ( true, true, _camMgr.camera );
				if ( !_nsSndr )
				{
					sRsrc = 'error_livecast_stream_null';
					bOk = false;
				}
			}
			catch ( err:Error )
			{
				sRsrc = 'error_livecast_publish';
				aParams = [ err.message ];
				bOk = false;
			}
			if ( bOk )
			{
				// stream has initialized successfully
				sRsrc = 'status_livecast_publish'
				_camMgr.dimensionsCheck ( );
			}
			sStat = resourceManager.getString ( 'default', sRsrc, aParams );
			debugOut ( sStat );
			_sessionMgr.statusSet ( sStat );
		}
		
		public function connectQueue ( preset:SfsPreset = null ) : void
		{
			if ( preset != null )
			{
				// called from connect view
				connectPreset = preset;
			}
			Security.loadPolicyFile ( 'xmlsocket://' + connectSettings.host + ':' + connectSettings.port );
			if ( _sf && _sf.isConnected )
			{
				_sessionMgr.viewStatePop ( 'connect_prompt_cs' );
				if ( !_sf.lastJoinedRoom )
				{
					_sessionMgr.viewStatePush ( 'login_prompt_cs' );
				}
			}
			else
			{
				callLater ( _Connect );
			}
		}
		
		/**
		 * @param verbose Boolean true if to report errors to user
		 * @return Boolean true if validates okay, false if not
		 */
		public function loginRequestQueue (
			robotName:String,
			pilotNames:String,
			assetsDir:String,
			verbose:Boolean = true
		) : Boolean
		{
			// validate
			var reEnds:RegExp = /^\s+|\s+$/g;
			var reExcess:RegExp = /\s{2,}/g;
			var sRobotName:String = robotName.replace ( reEnds, '' );
			sRobotName = sRobotName.replace ( reExcess, ' ' );
			var iLen:int = sRobotName.length;
			var bError:Boolean = false;
			var aLobbyNames:Array = [ 'Lobby', 'lobby', 'Lounge', 'lounge' ];
			var aPilots:Array = [];
			if ( iLen < 3 || iLen > 32 || aLobbyNames.indexOf ( sRobotName ) >= 0 )
			{
				// invalid Robot name
				bError = true;
			}
			else
			{
				var sPilots:String = pilotNames.replace ( reEnds, '' );
				sPilots = sPilots.replace ( reExcess, ' ' );
				if ( sPilots.length < 3 )
				{
					// invalid Pilot name
					bError = true;
				}
				else
				{
					aPilots = sPilots.split ( /\s*,\s*/ );
					var iLim:int = aPilots.length;
					var i_sName:String;
					for ( var i:int=0; i<iLim; i++ )
					{
						i_sName = aPilots [ i ];
						iLen = i_sName.length;
						if ( iLen < 3 || iLen > 32 || aLobbyNames.indexOf ( i_sName ) >= 0 )
						{
							// invalid Pilot name
							bError = true;
							break;
						}
					}
					sPilots = aPilots.join ( ',' );
				}
			}
			
			loginRobotName = sRobotName;
			loginPilotNames = sPilots;
			loginAssetsDir = assetsDir;
			
			if ( bError )
			{
				var sErr:String = resourceManager.getString ( 'default', 'error_login_name' );
				debugOut ( sErr );
				if ( verbose )
				{
					loginPrompt = sErr;
					// dispatchEvent ( new DialogEvent ( DialogEvent.ALERT, 'error_login_name', 'error_login_name_title' ) );
				}
				return false;
			}
			
			_vsPilots = Vector.<String> ( aPilots );
			
			// if get here, have reasonable entries
			loginPrompt = '';
			var userState:UserState = _sessionMgr.userState;
			userState.robotName = sRobotName;
			userState.pilotNames = sPilots;
			userState.assetsDir = assetsDir;
			// for now no passwords needed
			_LoginAttempt ( sRobotName );
			return true;
		}
		
		override public function roomVarsQueue ( vars:Vector.<MessageData>, immediate:Boolean = false ) : void
		{
			// only keeping the latest value if any repeats
			var i:int;
			var iLim:int = vars.length;
			var i_md:MessageData;
			for ( i=0; i<iLim; i++ )
			{
				i_md = vars [ i ];
				_roomVarsQueued [ i_md.message ] = i_md;
			}
			if ( immediate )
			{
				if ( _sf && _sf.isConnected )
				{
					_RoomVarsQueuedSend ( );
				}
			}
		}
		
		override public function sleep ( ) : void
		{
			super.sleep ( );
			if ( _sf && _sf.isConnected )
			{
				_sf.enableLagMonitor ( false );
			}
			_TimersCancelAll ( );
			_AvCastMgrDismiss ( );
		}
		
		override public function userVarsQueue ( vars:Vector.<MessageData>, immediate:Boolean = false ) : void
		{
			// only keeping the latest value if any repeats
			var i:int;
			var iLim:int = vars.length;
			var i_md:MessageData;
			for ( i=0; i<iLim; i++ )
			{
				i_md = vars [ i ];
				_userVarsQueued [ i_md.message ] = i_md;
			}
			if ( immediate )
			{
				if ( _sf && _sf.isConnected )
				{
					_UserVarsQueuedSend ( );
				}
			}
		}
		
		override public function wake ( ) : void
		{
			super.wake ( );
			if ( _sf && _sf.isConnected && _sf.lastJoinedRoom )
			{
				_sf.enableLagMonitor ( true );
				_tmrUserVars.start ( );
				_tmrRoomVars.start ( );
				callLater ( _AvCastMgrInit );
			}
		}
		
		
		// PROTECTED METHODS
		
		override protected function _debugChanged ( value:Boolean ) : void
		{
			super._debugChanged ( value );
			if ( _sf )
			{
				_sf.debug = value;
			}
		}
		
		
		// PRIVATE PROPERTIES
		
		private var _aPresets:Array;
		private var _avCastMgr:AVCastManagerArx;
		private var _camMgr:CameraManager;
		private var _iPilotUserId:int = 0;
		// will update _iSfsLagRT to latest average from SmartFox's lag monitoring
		private var _iSfsLagRT:int = 200;
		private var _nsRcvr:NetStream;
		private var _nsSndr:NetStream;
		private var _sf:SmartFox;
		private var _tmrConnect:Timer; // connection timeout
		private var _tmrDog:Timer; // watch dog timer
		private var _tmrRoomVars:Timer; // send queued room variables
		private var _tmrUserVars:Timer; // send queued user variables
		private var _vidRcvr:Video;
		private var _vidSndr:Video;
		private var _vsPilots:Vector.<String>;
		// private var _vsPilotsQueue:Vector.<String> = new <String> [];
		private var _wpsMgr:WaypointsManager;
		
		// PRIVATE METHODS
		
		private function _AppResized ( event:Event = null ) : void
		{
			avSenderPublish ( );
		}
		
		private function _AvCastMgrDismiss ( ) : void
		{
			if ( !_avCastMgr )
				return;
			
			avSenderClear ( );
			avReceiverClear ( );
			
			_avCastMgr.removeEventListener ( RedBoxConnectionEvent.AV_CONNECTION_INITIALIZED, _AvConnectionInited );
			_avCastMgr.removeEventListener ( RedBoxConnectionEvent.AV_CONNECTION_ERROR, _AvConnectionError );
			_avCastMgr.destroy ( );
			_avCastMgr = null;
		}
		
		private function _AvCastMgrInit ( ) : void
		{
			_sessionMgr.statusSetResource ( 'status_sfs_livecast_wait' );
			
			// instantiate AVCastManager
			_avCastMgr = new AVCastManagerArx ( _sf, _sf.currentIp, false, true );
			
			_avCastMgr.addEventListener ( RedBoxConnectionEvent.AV_CONNECTION_INITIALIZED, _AvConnectionInited );
			_avCastMgr.addEventListener ( RedBoxConnectionEvent.AV_CONNECTION_ERROR, _AvConnectionError );
		}
		
		private function _AvConnectionError ( event:RedBoxConnectionEvent ) : void
		{
			var sStat:String = resourceManager.getString ( 'default', 'error_av_connect', [ event.params.errorCode ] );
			debugOut ( sStat );
			_sessionMgr.statusSet ( sStat );
		}
		
		private function _AvConnectionInited ( event:RedBoxConnectionEvent ) : void
		{
			avSenderPublish ( );
			avReceiverSubscribe ( );
		}
		
		private function _AvSenderCameraChanged ( event:Event = null ) : void
		{
			avSenderPublish ( );
		}
		
		private function _AvSenderConfigChanged ( event:Event = null ) : void
		{
			if ( _sleeping )
				return;
			
			if ( !_nsSndr )
			{
				avSenderPublish ( );
			}
		}
		
		private function _AvSenderDimensionsReady ( event:Event = null ) : void
		{
			// Tell Control Panel how much to rotate video
			userVarsQueue ( new <MessageData> [
				new MessageData ( 'cvr', _sessionMgr.videoSenderRotation )
			] );
			// if monitoring outbound video, update its dimensions
			_AvSenderMonitorUpdate ( );
		}
		
		private function _AvSenderMonitorUpdate ( event:Event = null ) : void
		{
			_AvSenderMonitorClear ( );
			
			if ( !_camMgr.monitor )
				return;
			
			if ( !_avCastMgr )
				return;
			
			if ( !_avCastMgr.isConnected )
				return;
			
			var iVidPodHt:int = _sessionMgr.videoPodHeight;
			var iVidPodWd:int = _sessionMgr.videoPodWidth;
			var iVidHt:int = _camMgr.cameraHeight;
			var iVidWd:int = _camMgr.cameraWidth;
			var nVidHtRot:Number;
			var nVidWdRot:Number;
			var nVidHtPost:Number;
			var nVidWdPost:Number;
			var nScale:Number;
			if ( _sessionMgr.orientedPortrait )
			{
				nVidHtRot = iVidWd;
				nVidWdRot = iVidHt;
			}
			else
			{
				nVidHtRot = iVidHt;
				nVidWdRot = iVidWd;
			}
			if ( _sessionMgr.videoPodAspectRatio < ( nVidHtRot / nVidWdRot ) )
			{
				// height is limiting
				nScale = _camMgr.insetSize * iVidPodHt / nVidHtRot;
			}
			else
			{
				// width is limiting
				nScale = _camMgr.insetSize * iVidPodWd / nVidWdRot;
			}
			// calculate post-transform dimensions
			nVidHtPost = nScale * nVidHtRot;
			nVidWdPost = nScale * nVidWdRot;
			// create transform matrix
			// to position the video in the videoPod
			var mat:Matrix = new Matrix ( );
			// move center to origin
			mat.translate ( -iVidWd / 2, -iVidHt / 2 );
			// apply scale
			mat.scale ( nScale, nScale );
			// apply rotation if necessary
			if ( _sessionMgr.orientedPortrait )
			{
				mat.rotate ( _sessionMgr.videoSenderRotation * MathConsts.DEGREES_TO_RADIANS );
			}
			// translate as dictated by alignment settings
			var oAlignVals:Object = {
				'left': -0.5, 'center': 0, 'right': 0.5,
				'top': -0.5, 'middle': 0, 'bottom': 0.5
			};
			// X translated by 1/2 pod width will center video horizontally.
			// To align left or right edges respectively, subtract or add
			// 1/2 the difference between pod width and video width.
			// Y translated by 1/2 pod height will center video vertically.
			// To align top or bottom edges respectively, subtract or add
			// 1/2 the difference between pod height and video height.
			mat.translate (
				iVidPodWd / 2 + ( iVidPodWd - nVidWdPost ) * ( oAlignVals [ _camMgr.insetAlignH ] as Number ),
				iVidPodHt / 2 + ( iVidPodHt - nVidHtPost ) * ( oAlignVals [ _camMgr.insetAlignV ] as Number )
			);
			// display video
			_vidSndr = new Video ( iVidWd, iVidHt );
			_vidSndr.transform.matrix = mat;
			_sessionMgr.videoPod.addChild ( _vidSndr );
			_vidSndr.attachCamera ( _camMgr.camera );
		}
		
		private function _AvSenderMonitorClear ( ) : void
		{
			if ( !_vidSndr )
				return;
			
			_vidSndr.attachCamera ( null );
			_vidSndr.clear ( );
			_sessionMgr.videoPod.removeChild ( _vidSndr );
			_vidSndr = null;
		}
		
		private function _CameraRestartRequest ( ) : void
		{
			if ( _sessionMgr.moveIgnore )
			{
				debugOut ( 'cameraRestartRequest discarded due to emergency flags: ' + _sessionMgr.emergencyFlagsGet() );
				return;
			}
			
			callLater ( avSenderPublish );
		}
		
		private function _Connect ( ) : void
		{
			_sessionMgr.statusSetResource ( 'status_sfs_connect_wait', true );
			_ConnectTimerInit ( );
			_sf.connectWithConfig ( connectSettings.configData );
			_sessionMgr.viewStatePop ( 'connect_prompt_cs' );
		}
		
		private function _Connection ( event:SFSEvent ) : void
		{
			_ConnectTimerDismiss ( );
			var sStat:String;
			if ( event.params.success )
			{
				sStat = resourceManager.getString ( 'default', 'status_sfs_connect' );
				_sessionMgr.statusSet ( sStat );
				debugOut ( sStat );
				_sessionMgr.viewStatePush ( 'login_prompt_cs' );
				/*
				var userState:UserState = _sessionMgr.userState;
				if ( !loginRequestQueue (
					userState.robotName,
					userState.pilotNames,
					userState.assetsDir,
					false
				) )
				{
					// parameters did not validate,
					// so need to enable login view
					_sessionMgr.appState = 'login_prompt_cs';
				}
				*/
			}
			else
			{
				connectPrompt = resourceManager.getString ( 'default', 'error_sfs_connect', [ event.params.errorMessage ] );
				_sessionMgr.statusSet ( connectPrompt );
				debugOut ( connectPrompt );
				_sessionMgr.viewStatePush ( 'connect_prompt_cs' );
			}
		}
		
		private function _ConnectionLost ( event:SFSEvent ) : void
		{
			_TimersCancelAll ( );
			_AvCastMgrDismiss ( );
			connectPrompt = resourceManager.getString ( 'default', 'error_sfs_connect_lost', [ event.params.reason ] );
			_sessionMgr.statusSet ( connectPrompt );
			debugOut (connectPrompt );
			_sessionMgr.viewStatePush ( 'connect_prompt_cs' );
		}
		
		private function _ConnectTimeout ( event:TimerEvent ) : void
		{
			_ConnectTimerDismiss ( );
			_sf.disconnect ( ); // just in case of a race condition
			connectPrompt = resourceManager.getString ( 'default', 'error_sfs_connect_timeout' );
			_sessionMgr.statusSet ( connectPrompt );
			debugOut ( connectPrompt );
			_sessionMgr.viewStatePush ( 'connect_prompt_cs' );
		}
		
		private function _ConnectTimerDismiss ( ) : void
		{
			if ( !_tmrConnect )
				return;
			
			_tmrConnect.stop ( );
			_tmrConnect.removeEventListener ( TimerEvent.TIMER, _ConnectTimeout );
			_tmrConnect = null;
		}
		
		private function _ConnectTimerInit ( ) : void
		{
			if ( !_tmrConnect )
			{
				_tmrConnect = new Timer ( 10000, 1 );
				_tmrConnect.addEventListener ( TimerEvent.TIMER, _ConnectTimeout );
			}
			_tmrConnect.reset ( );
			_tmrConnect.start ( );
		}
		
		private function _HaveValidPilotId ( ) : Boolean
		{
			
			if ( _iPilotUserId < 1 )
				return false;
			
			if ( _sf.lastJoinedRoom )
			{
				if ( _sf.lastJoinedRoom.getUserById ( _iPilotUserId ) )
				{
					return true;
				}
			}
			
			_iPilotUserId = 0;
			return false;
		}
		
		private function _JoinFailure ( event:SFSEvent ) : void
		{
			debugOut ( 'error_sfs_join', true, [ event.params.errorMessage ], true );
			
			// ##### TODO #####
			callLater ( _Logout );
		}
		
		private function _JoinSuccess ( event:SFSEvent ) : void
		{
			var sStat:String = resourceManager.getString ( 'default', 'status_sfs_join', [ event.params.room.name ] );
			debugOut ( sStat );
			_sessionMgr.statusSet ( sStat );
			// prepare for av feeds
			callLater ( _AvCastMgrInit );
			// initialize user and room variables
			_sessionMgr.batchVarsReportQueue ( );
			// start timers
			_sf.enableLagMonitor ( true );
			_tmrUserVars.start ( );
			_tmrRoomVars.start ( );
		}
		
		private function _LagMonitorUpdate ( event:SFSEvent ) : void
		{
			_iSfsLagRT = event.params.lagValue;
		}
		
		private function _LoginAttempt ( userName:String = '', password:String = null, zoneName:String = null, params:ISFSObject = null ) : void
		{
			_sessionMgr.viewStatePop ( 'login_prompt_cs' );
			_sessionMgr.statusSetResource ( 'status_sfs_login_wait', true );
			_sf.send ( new LoginRequest ( userName, password, zoneName, params ) );
		}
		
		private function _LoginFailed ( event:SFSEvent ) : void
		{
			var sErr:String = resourceManager.getString ( 'default', 'error_sfs_login', [ event.params.errorMessage ] );
			debugOut ( sErr );
			loginPrompt = sErr;
			if ( _sf.isConnected )
				_sessionMgr.viewStatePush ( 'login_prompt_cs' );
			else
				_sessionMgr.viewStatePush ( 'connect_prompt_cs' );
		}
		
		private function _LoginSucceeded ( event:SFSEvent ) : void
		{
			_sessionMgr.statusSetResource ( 'status_sfs_join_wait', true );
			var sRobotName:String = _sessionMgr.userState.robotName;
			// debugOut ( 'status_sfs_login', true, [ event.params.user.name ] );
			if ( _sf.roomManager.containsRoom ( sRobotName ) )
			{
				// should not happen, but just in case...
				_sf.send ( new JoinRoomRequest ( sRobotName ) );
			}
			else
			{
				// create room and auto-join
				var re:RoomEvents = new RoomEvents ( );
				re.allowUserCountChange = true;
				re.allowUserEnter = true;
				re.allowUserExit = true;
				re.allowUserVariablesUpdate = true
				var rp:RoomPermissions = new RoomPermissions ( );
				rp.allowPublicMessages = true;
				var rs:RoomSettings = new RoomSettings ( sRobotName );
				rs.events = re;
				rs.permissions = rp;
				rs.maxUsers = 100;
				rs.maxVariables = 100;
				
				_sf.send ( new CreateRoomRequest ( rs, true ) );
			}
		}
		
		private function _Logout ( ) : void
		{
			if ( _sf.isConnected )
				_sf.send ( new LogoutRequest ( ) );
			else
				_sessionMgr.viewStatePush ( 'connect_prompt_cs' );
		}
		
		private function _LogoutDone ( event:SFSEvent ) : void
		{
			_sessionMgr.viewStatePush ( 'login_prompt_cs' );
		}
		
		private function _PilotCheck ( room:Room, userLeaving:User = null ) : void
		{
			if ( room != _sf.lastJoinedRoom )
				return;
			
			var bPiloted:Boolean = room.getVariable ( 'pib' ).getBoolValue ( );
			var sPilot:String = room.getVariable ( 'pic' ).getStringValue ( );
			if ( !bPiloted )
			{
				_iPilotUserId = 0;
				_sessionMgr.emergencyFlagsClear ( EmergencyFlags.LATENCY );
			}
			else if ( room.getUserByName ( sPilot ) == null || ( userLeaving != null && userLeaving.name == sPilot ) )
			{
				// pilot exited room
				var aRoomVars:Array = [
					new SFSRoomVariable ( 'pib', false ),
					new SFSRoomVariable ( 'pic', '' )
				];
				_sf.send ( new SetRoomVariablesRequest ( aRoomVars ) );
				_iPilotUserId = 0;
				_sessionMgr.emergencyFlagsClear ( EmergencyFlags.LATENCY );
			}
		}
		
		private function _PingReceived ( sfso:ISFSObject ) : void
		{
			var ping:Ping = Ping.newFromSFSObject ( sfso );
			ping.robotTimeStamp = new Date().getTime();
			ping.robotLagSfs = _iSfsLagRT;
			_sf.send ( new PrivateMessageRequest ( 'pong', _iPilotUserId, ping.toSFSObject() ) );
			if ( _sleeping )
			{
				if ( _sessionMgr.batterySupported )
				{
					userVarsQueue (
						new <MessageData> [
							new MessageData ( 'pbt', _sessionMgr.batteryPct )
						],
						true
					);
				}
			}
			_WatchDogIntervalSet ( ping.intervalMsecs );
		}
		
		private function _PresetsLoad ( ) : void
		{
			connectPrompt = resourceManager.getString ( 'default', 'sfs_connect_prompt' );
			var f:File = File.applicationDirectory.resolvePath ( 'config/sfs-presets.json' );
			var sStat:String;
			if ( !f.exists )
			{
				sStat = resourceManager.getString ( 'default', 'error_sfs_presets' );
				debugOut ( sStat );
				_sessionMgr.statusSet ( sStat );
				_sessionMgr.viewStatePush ( 'connect_prompt_cs' );
				return;
			}
			
			// build arrayList of presets, and pre-select the default one
			var sIdDef:String = '';
			_aPresets = [];
			var spDef:SfsPreset;
			var spTyp:SfsPreset;
			
			try
			{
				var fs:FileStream = new FileStream();
				// open
				fs.open ( f, FileMode.READ );
				
				// read
				var sJson:String = fs.readUTFBytes ( fs.bytesAvailable );
				
				// close
				fs.close ( );
				
				// debugOut ( sJson );
				
				var oJson:Object = JSON.parse ( sJson );
				
				if ( oJson.hasOwnProperty ( 'configs' ) )
				{
					if ( oJson.hasOwnProperty ( 'default' ) )
					{
						sIdDef = oJson [ 'default' ];
					}
					var iLabelLen:int = 0;
					var oCfgs:Object = oJson [ 'configs' ];
					var i_sId:String;
					var i_sLoc:String;
					var i_iLen:int;
					var i_sp:SfsPreset;
					for ( i_sId in oCfgs )
					{
						i_sp = new SfsPreset ( i_sId, oCfgs [ i_sId ] )
						_aPresets.push ( i_sp );
						if ( i_sId == sIdDef )
						{
							spDef = i_sp;
						}
						i_sLoc = resourceManager.getString ( 'default', 'sfs_connect_preset_' + i_sId );
						if ( !i_sLoc )
						{
							i_sLoc = i_sId;
						}
						i_iLen = i_sLoc.length;
						if ( i_iLen > iLabelLen )
						{
							iLabelLen = i_iLen;
							spTyp = i_sp;
						}
					}
				}
			}
			catch ( err:Error )
			{
				sStat = resourceManager.getString ( 'default', 'error_sfs_presets_load', [ err.message ] );
				_sessionMgr.statusSet ( sStat );
				debugOut ( sStat, false, null, true );
				_sessionMgr.viewStatePush ( 'connect_prompt_cs' );
				return;
			}
			
			_aPresets.sortOn ( 'id', Array.CASEINSENSITIVE );
			connectPreset = spDef;
			connectPresetTypical = spTyp;
			
			// if get here, can try autoconnect
			connectQueue ( );
		}
		
		private function _PrivateMessageReceived ( event:SFSEvent ) : void
		{
			var sender:User = event.params.sender;
			if ( sender == _sf.mySelf )
				return;
			
			// validate sender so control messages are only accepted from pilot
			var sSenderName:String = sender.name;
			if ( _vsPilots.indexOf ( sSenderName ) < 0 )
				return;
			
			_iPilotUserId = sender.id;
			
			if ( !_sleeping )
			{
				_WatchDogReset ( );
			}
			
			var sMsg:String = event.params.message;
			switch ( sMsg )
			{
				case 'ping':
				{
					_PingReceived ( event.params.data );
					break;
				}
				case 'emsack':
				{
					_sessionMgr.emergencyAcknowledge ( );
					break;
				}
				case 'mv':
				{
					_sessionMgr.motionRequest ( MoveProps.NewFromSFSObject ( event.params.data ) );
					break;
				}
				case 'cu':
				{
					_sessionMgr.customRequest ( MessageByteArray.NewFromSFSObject ( event.params.data ) );
					break;
				}
				case 'cm':
				{
					_sessionMgr.cameraMoveRequest ( CameraMove.NewFromSFSObject ( event.params.data ) );
					break;
				}
				case 'ch':
				{
					_sessionMgr.cameraMoveHomeRequest ( );
					break;
				}
				case 'cr':
				{
					_sessionMgr.cameraMoveResetRequest ( );
					break;
				}
				case 'wp':
				{
					_wpsMgr.waypointAdd ( WaypointCoordinates.NewFromSFSObject ( event.params.data ) );
					break;
				}
				case 'wp0':
				{
					_wpsMgr.active = false;
					break;
				}
				case 'wp1':
				{
					_wpsMgr.active = true;
					break;
				}
				case 'wpca':
				{
					_wpsMgr.listClear ( );
					break;
				}
				case 'wpd':
				{
					_wpsMgr.waypointDelete ( MessageByteArray.NewFromSFSObject ( event.params.data ) );
					break;
				}
				case 'wpm':
				{
					_wpsMgr.waypointMove ( WaypointCoordinates.NewFromSFSObject ( event.params.data ) );
					break;
				}
				case 'wpla':
				{
					_wpsMgr.listAppend ( WaypointsList.NewFromSFSObject ( event.params.data ) );
					break;
				}
				case 'wplr':
				{
					_wpsMgr.listReplace ( WaypointsList.NewFromSFSObject ( event.params.data ) );
					break;
				}
				case 'fl':
				{
					_sessionMgr.light ( event.params.data.getBool ( 'b' ) );
					break;
				}
				case 'camerarestart':
				{
					_CameraRestartRequest ( );
					break;
				}
				case 'cameraconfig':
				{
					_camMgr.configRequest ( CameraConfig.NewFromSFSObject ( event.params.data ) );
					break;
				}
				case 'cameraconfigadjust':
				{
					_camMgr.configAdjustRequest ( event.params.data.getBool ( 'b' ) );
					break;
				}
				case 'cameraconfigdef':
				{
					_camMgr.configDefaultRequest ( CameraConfig.NewFromSFSObject ( event.params.data ) );
					break;
				}
				case 'cameraconfigmotion':
				{
					_camMgr.configMotionRequest ( CameraConfig.NewFromSFSObject ( event.params.data ) );
					break;
				}
				case 'camerafpspollmsec':
				{
					_camMgr.fpsPollRequest ( event.params.data.getShort ( 'm' ) || 0 );
					break;
				}
				case 'sleep':
				{
					_sessionMgr.sleepRequest ( );
					break;
				}
				case 'wake':
				{
					_sessionMgr.wakeRequest ( );
					break;
				}
				case 'exit':
				{
					_sessionMgr.exitConfirmed ( );
					break;
				}
				default:
				{
					debugOut ( 'error_control_message', true, [ sMsg ] );
					break;
				}
			}
		}
		
		private function _RoomCreationError ( event:SFSEvent ) : void
		{
			debugOut ( 'error_sfs_room_creation', true, [ event.params.errorMessage ] );
			// now what?
			// ##### TODO
		}
		
		private function _RoomVarsQueuedSend ( event:TimerEvent = null ) : void
		{
			var vars:Array = [];
			for each ( var i_md:MessageData in _roomVarsQueued )
			{
				if ( i_md.typeIsComplex )
				{
					vars.push ( new SFSRoomVariable ( i_md.message, i_md.data.toSFSObject() ) );
				}
				else
				{
					vars.push ( new SFSRoomVariable ( i_md.message, i_md.data ) );
				}
			}
			if ( vars.length > 0 )
			{
				// prepare for next batch
				_roomVarsQueued = {};
				// send all the queued variables
				_sf.send ( new SetRoomVariablesRequest ( vars ) );
			}
		}
		
		private function _TimersCancelAll ( ) : void
		{
			_ConnectTimerDismiss ( );
			_WatchDogCancel ( );
			_tmrRoomVars.stop ( );
			_tmrUserVars.stop ( );
		}
		
		private function _UserCountChanged ( event:SFSEvent ) : void
		{
			_PilotCheck ( event.params.room );
		}
		
		private function _UserEnteredRoom ( event:SFSEvent ) : void
		{
			_PilotCheck ( event.params.room );
		}
		
		private function _UserExitedRoom ( event:SFSEvent ) : void
		{
			var room:Room = event.params.room;
			var user:User = event.params.user;
			if ( room.name == _sessionMgr.userState.robotName &&
				user == _sf.mySelf )
			{
				_sf.enableLagMonitor ( false );
				_TimersCancelAll ( );
				_AvCastMgrDismiss ( );
			}
			else
			{
				_PilotCheck ( event.params.room, event.params.user );
			}
		}
		
		private function _UserVarsQueuedSend ( event:TimerEvent = null ) : void
		{
			var vars:Array = [];
			for each ( var i_md:MessageData in _userVarsQueued )
			{
				if ( i_md.typeIsComplex )
				{
					vars.push ( new SFSUserVariable ( i_md.message, i_md.data.toSFSObject() ) );
				}
				else
				{
					vars.push ( new SFSUserVariable ( i_md.message, i_md.data ) );
				}
			}
			if ( vars.length > 0 )
			{
				// prepare for next batch
				_userVarsQueued = {};
				// send all the queued variables
				_sf.send ( new SetUserVariablesRequest ( vars ) );
			}
		}
		
		private function _WatchDogBark ( event:TimerEvent ) : void
		{
			// Watch dog timer lapsed.
			debugOut ( 'status_watchdog', true, [ _tmrDog.delay ] );
			if ( _HaveValidPilotId ( ) )
			{
				_sessionMgr.emergencyFlagsSet ( EmergencyFlags.LATENCY );
			}
		}
		
		private function _WatchDogCancel ( ) : void
		{
			_tmrDog.stop ( );
		}
		
		private function _WatchDogIntervalSet ( pingIntervalMsecs:int ) : void
		{
			_tmrDog.reset ( );
			// allow a cushion on top of time until next expected ping
			var iNew:int = pingIntervalMsecs * 1.5 + _iSfsLagRT;
			if ( _tmrDog.delay != iNew )
				_tmrDog.delay = iNew;
			_tmrDog.start ( );
		}
		
		private function _WatchDogReset ( ) : void
		{
			_tmrDog.reset ( );
			_tmrDog.start ( );
		}
	}
}