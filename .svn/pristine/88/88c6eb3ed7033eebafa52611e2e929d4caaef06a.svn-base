package vo
{
	import com.smartfoxserver.v2.entities.data.ISFSObject;
	import com.smartfoxserver.v2.entities.data.SFSObject;
	
	import flash.utils.ByteArray;
	import flash.utils.IDataInput;
	import flash.utils.IDataOutput;
	import flash.utils.IExternalizable;
	
	import utils.HexStringUtil;

	/**
	 * Note: Implements IExternalizable, since Command ID and parameter value(s) are already stored as a Byte Array.
	 */
	public class ArduinoCommand implements IExternalizable
	{
		// CONSTANTS
		
		//   command IDs
		//											Data[0] =  CMD TYPE | Qual | Arguments
		//													bit  7654321   0    Bytes    N = 1 + bytes
		public static const MOVE:uint =					0x01; // 0000000   1     4      
		public static const CAMERA_MOVE:uint =			0x02; // 0000001   0     4
		public static const CAMERA_HOME:uint =			0x04; // 0000010   0     0
		public static const CAMERA_RESET:uint =			0x05; // 0000010   1     0
		public static const READ_EEPROM:uint =			0x06; // 0000011   0     3
		public static const WRITE_EEPROM:uint =			0x07; // 0000011   1     3 + b
		public static const SAFE_ROVER:uint =			0x08; // 0000100   0     0
		public static const SLEEP:uint =				0x0A; // 0000101   0     0
		public static const WAKEUP:uint =				0x0B; // 0000101   1     0
		public static const HEADLIGHT_OFF:uint =		0x0C; // 0000110   0     0
		public static const HEADLIGHT_ON:uint =			0x0D; // 0000110   1     0
		public static const PING_INTERVAL:uint =		0x10; // 0001000   0     4 (unsigned long) ms
		public static const PING:uint =					0x11; // 0001000   1     0
		public static const HEADING:uint =				0x12; // 0001001   0     2 (unsigned short) degrees
		public static const CURRENT_COORD:uint =		0x13; // 0001001   1     8 (float) lat (float) lon
		public static const WAYPOINT_COORD:uint =		0x14; // 0001010   0     9 (float) lat (float) lon (unsigned byte) admin ID
		public static const WAYPOINTS_OFF:uint =		0x16; // 0001011   0     0
		public static const WAYPOINTS_ON:uint =			0x17; // 0001011   1     0
		public static const WAYPOINTS_CLEAR:uint =		0x18; // 0001100   0     0
		public static const WAYPOINT_MOVE:uint =		0x19; // 0001100   1     9 (float) lat (float) lon (unsigned byte) admin ID
		public static const WAYPOINT_DELETE:uint =		0x1A; // 0001101   0     1 (unsigned byte) admin ID
		
		
		// PUBLIC PROPERTIES AND GET/SET METHOD GROUPS
		
		public function get boolean ( ) : Boolean
		{
			if ( !_ReadyToReadValue ( 1 ) )
				return false;
			return _commandBytes.readBoolean ( );
		}
		public function set boolean ( val:Boolean ) : void
		{
			if ( _ReadyToWriteValue ( ) )
				_commandBytes.writeBoolean ( val );
		}
		
		public function getBooleanAt ( position:uint ) : Boolean
		{
			if ( !_ReadyToReadValue ( 1, position ) )
				return false;
			return _commandBytes.readBoolean ( );
		}
		public function setBooleanAt ( val:Boolean, position:uint ) : void
		{
			if ( _ReadyToWriteValue ( position ) )
				_commandBytes.writeBoolean ( val );
		}
		
		public function get byte ( ) : int
		{
			if ( !_ReadyToReadValue ( 1 ) )
				return 0;
			return _commandBytes.readByte ( );
		}
		public function set byte ( val:int ) : void
		{
			if ( _ReadyToWriteValue ( ) )
				_commandBytes.writeByte ( val );
		}
		
		public function getByteAt ( position:uint ) : int
		{
			if ( !_ReadyToReadValue ( 1, position ) )
				return 0;
			return _commandBytes.readByte ( );
		}
		public function setByteAt ( val:int, position:uint ) : void
		{
			if ( _ReadyToWriteValue ( position ) )
				_commandBytes.writeByte ( val );
		}
		
		/**
		 * Entire command ByteArray to be sent to Arduino, or status received
		 * from Arduino, including commandId and any argument bytes
		 */
		public function get commandBytes ( ) : ByteArray
		{
			return _commandBytesClone ( );
		}
		public function set commandBytes ( val:ByteArray ) : void
		{
			_commandString = '';
			_commandBytes = val;
		}
		
		/**
		 * Command identifier byte
		 */
		public function get commandId ( ) : uint
		{
			if ( _commandBytes.length < 1 )
				return 0; // return
			
			return _commandBytes [ 0 ];
		}
		public function set commandId ( val:uint ) : void
		{
			_commandString = '';
			_commandBytes [ 0 ] = val;
		}
		
		/**
		 * Command identifier byte as hex string
		 */
		public function get commandIdHex ( ) : String
		{
			var s:String = commandId.toString ( 16 ).toUpperCase ( );
			while ( s.length < 2 )
			{
				s = '0' + s;
			}
			return '0x' + s;
		}
		
		/**
		 * Command identifier byte as hex (dec) string
		 */
		public function get commandIdHexDec ( ) : String
		{
			var uId:uint = commandId;
			var s:String = uId.toString ( 16 ).toUpperCase ( );
			while ( s.length < 2 )
			{
				s = '0' + s;
			}
			return '0x' + s + ' (' + uId.toString() + ')';
		}
		
		public function get commandString ( ) : String
		{
			if ( _commandString.length < 1 )
			{
				// need to repopulate string
				_commandString = HexStringUtil.HexStringFromByteArray ( _commandBytes );
			}
			return _commandString;
		}
		
		public function get double ( ) : Number
		{
			if ( !_ReadyToReadValue ( 8 ) )
				return NaN;
			return _commandBytes.readDouble ( );
		}
		public function set double ( value:Number ) : void
		{
			if ( _ReadyToWriteValue ( ) )
				_commandBytes.writeDouble ( value );
		}
		
		public function getDoubleAt ( position:uint ) : Number
		{
			if ( !_ReadyToReadValue ( 8, position ) )
				return NaN;
			return _commandBytes.readDouble ( );
		}
		public function setDoubleAt ( value:Number, position:uint ) : void
		{
			if ( _ReadyToWriteValue ( position ) )
				_commandBytes.writeDouble ( value );
		}
		
		public function get float ( ) : Number
		{
			if ( !_ReadyToReadValue ( 4 ) )
				return NaN;
			return _commandBytes.readFloat ( );
		}
		public function set float ( value:Number ) : void
		{
			if ( _ReadyToWriteValue ( ) )
				_commandBytes.writeFloat ( value );
		}
		
		public function getFloatAt ( position:uint ) : Number
		{
			if ( !_ReadyToReadValue ( 4, position ) )
				return NaN;
			return _commandBytes.readFloat ( );
		}
		public function setFloatAt ( value:Number, position:uint ) : void
		{
			if ( _ReadyToWriteValue ( position ) )
				_commandBytes.writeFloat ( value );
		}
		
		public function get short ( ) : int
		{
			if ( !_ReadyToReadValue ( 2 ) )
				return 0;
			return _commandBytes.readShort ( );
		}
		public function set short ( val:int ) : void
		{
			if ( _ReadyToWriteValue ( ) )
				_commandBytes.writeShort ( val );
		}
		
		public function getShortAt ( position:uint ) : int
		{
			if ( !_ReadyToReadValue ( 2, position ) )
				return 0;
			return _commandBytes.readShort ( );
		}
		public function setShortAt ( val:int, position:uint ) : void
		{
			if ( _ReadyToWriteValue ( position ) )
				_commandBytes.writeShort ( val );
		}
		
		public function get unsignedByte ( ) : uint
		{
			if ( !_ReadyToReadValue ( 1 ) )
				return 0;
			return _commandBytes.readUnsignedByte ( );
		}
		public function set unsignedByte ( val:uint ) : void
		{
			if ( _ReadyToWriteValue ( ) )
				_commandBytes.writeByte ( val );
		}
		
		public function getUnsignedByteAt ( position:uint ) : uint
		{
			if ( !_ReadyToReadValue ( 1, position ) )
				return 0;
			return _commandBytes.readUnsignedByte ( );
		}
		public function setUnsignedByteAt ( val:uint, position:uint ) : void
		{
			if ( _ReadyToWriteValue ( position ) )
				_commandBytes.writeByte ( val );
		}
		
		public function get unsignedShort ( ) : uint
		{
			if ( !_ReadyToReadValue ( 2 ) )
				return 0;
			return _commandBytes.readUnsignedShort ( );
		}
		public function set unsignedShort ( val:uint ) : void
		{
			if ( _ReadyToWriteValue ( ) )
				_commandBytes.writeShort ( val );
		}
		
		public function getUnsignedShortAt ( position:uint ) : uint
		{
			if ( !_ReadyToReadValue ( 2, position ) )
				return 0;
			return _commandBytes.readUnsignedShort ( );
		}
		public function setUnsignedShortAt ( val:uint, position:uint ) : void
		{
			if ( _ReadyToWriteValue ( position ) )
				_commandBytes.writeShort ( val );
		}
		
		
		// CONSTRUCTOR AND SERIALIZATION/DESERIALIZATION
		
		/**
		 * If do not have bytes, call static method <strong>NewFromSFSObject</strong>
		 * @param bytes
		 */		
		public function ArduinoCommand ( bytes:ByteArray = null )
		{
			if ( bytes != null )
				_commandBytes = bytes;
		}
		
		public function readExternal ( input:IDataInput ) : void
		{
			_commandBytes = input.readObject ( );
		}
		
		public function writeExternal ( output:IDataOutput ) : void
		{
			output.writeObject ( _commandBytes );
		}
		
		
		// OTHER PUBLIC METHODS
		
		/**
		 * @return SFSObject containing command byte array
		 */
		public function toSFSObject ( ) : ISFSObject
		{
			var sfso:ISFSObject = new SFSObject ( );
			sfso.putByteArray ( 'b', _commandBytes );
			return sfso;
		}
		
		public function writeValueBytes ( byteArray:ByteArray, byteCount:uint ) : Boolean
		{
			if ( byteArray.bytesAvailable < byteCount )
				return false; // return
			
			if ( _commandBytes.length > 0 )
			{
				_commandString = '';
				byteArray.readBytes ( _commandBytes, 1, byteCount );
			}
			
			return true;
		}
		
		
		// PUBLIC STATIC METHODS
		
		public static function NewFromSFSObject ( sfso:ISFSObject ) : ArduinoCommand
		{
			return new ArduinoCommand ( sfso.getByteArray ( 'b' ) );
		}
		
		
		// PROTECTED PROPERTIES AND GET/SET METHOD GROUPS
		
		protected var _commandBytes:ByteArray = new ByteArray ( );
		protected var _commandString:String = '';
		
		
		// PROTECTED METHODS
		
		protected function _commandBytesClone ( ) : ByteArray
		{
			var ba:ByteArray = new ByteArray ( );
			ba.writeBytes ( _commandBytes );
			return ba;
		}
		
		protected function _commandStringClear ( ) : void
		{
			_commandString = '';
		}
		
		
		// PRIVATE METHODS
		
		// validates whether enough bytes are available, and sets position for read
		private function _ReadyToReadValue ( dataLength:uint, position:uint = 1 ) : Boolean
		{
			if ( _commandBytes.length >= position + dataLength )
			{
				_commandBytes.position = position;
				return true;
			}
			
			return false;
		}
		
		private function _ReadyToWriteValue ( position:uint = 1 ) : Boolean
		{
			if ( _commandBytes.length < position )
				return false;
			
			_commandString = '';
			_commandBytes.position = position;
			return true;
		}
		
	}
}